@echo off
setlocal EnableDelayedExpansion
REM Called as this:
REM <this>.bat $(SolutionDir) $(ProjectDir) $(BuildConf) $(PlatformName) $(TargetDir) $(TargetFileNam) $(TargetName) $(IntrmDir)

set batfilenam=%~n0%~x0
set batdir=%~dp0
set batdir=%batdir:~0,-1%
set _vspgINDENTS=%_vspgINDENTS%.
call :Echos START from %batdir%


REM _SolutionDir_ has double-quotes around, SolutionDir has no quotes.
set _SolutionDir_=%1&set SolutionDir=%~1
set _ProjectDir_=%2&set ProjectDir=%~2
set _BuildConf_=%3&set BuildConf=%~3
set _PlatformName_=%4&set PlatformName=%~4
set _TargetDir_=%5&set TargetDir=%~5
set _TargetFilenam_=%6&set TargetFilenam=%~6
set _TargetName_=%7&set TargetName=%~7
set _IntrmDir_=%8&set IntrmDir=%~8

set ExeDllDir=%TargetDir%

REM == debugging purpose ==
REM call :EchoVar SolutionDir
REM call :EchoVar ProjectDir
REM call :EchoVar _BuildConf_
REM call :EchoVar BuildConf
REM call :EchoVar IntrmDir
REM call :EchoVar TargetDir
REM call :EchoVar PlatformName
REM call :EchoVar TargetName

REM ==== Prelude Above ====

set AGILE_COPY_PATTERNS="%ExeDllDir%\%TargetFilenam%"
set AGILE_BARN_DIRS=d:\barn "d:\barn output"
rem -- The above two should match those in PostBuild-CopyOutput4.bat .
rem    To ensure they always match, you can define them in VSPU-StartEnv.bat, once and for all.

if not defined AGILE_BARN_DIRS (
	call :Echos AGILE_BARN_DIRS is empty, nothing to clean.
	exit /b 0
)

if not defined AGILE_COPY_PATTERNS (
	call :Echos [ERROR] AGILE_COPY_PATTERNS is empty.
	exit /b 4
)


REM For the two AGILE_xxx, escape double-quotes for wrapping into a single bat-parameter.
call :PackDoubleQuotes %AGILE_COPY_PATTERNS%
set __SourcePatterns__=%_vspg_dqpacked%
rem
call :PackDoubleQuotes %AGILE_BARN_DIRS%
set __DestDirs__=%_vspg_dqpacked%

set DestSubdir=__%BuildConf%\%PlatformName%

set VSPG_COPYFILE_DO_DELETE=1
call "%bootsdir%\AgileCopyOrDelete.bat"  "%ExeDllDir%"  "%__SourcePatterns__%"  "%__DestDirs__%"  "%DestSubdir%"

exit /b %ERRORLEVEL%


REM =============================
REM ====== Functions Below ======
REM =============================

:Echos
  REM This function preserves %ERRORLEVEL% for the caller,
  REM and, LastError does NOT pollute the caller.
  setlocal & set LastError=%ERRORLEVEL%
  echo %_vspgINDENTS%[%batfilenam%] %*
exit /b %LastError%

:EchoAndExec
  echo %_vspgINDENTS%[%batfilenam%] EXEC: %*
  %*
exit /b %ERRORLEVEL%

:EchoVar
  setlocal & set Varname=%~1
  call echo %_vspgINDENTS%[%batfilenam%] %Varname% = %%%Varname%%%
exit /b 0

:SetErrorlevel
  REM Usage example:
  REM call :SetErrorlevel 4
exit /b %1

:PackDoubleQuotes
  REM Take whole %* as input, replace each " with "" and return the result string.
  REM The return value is put in global var _vspg_dqpacked .
  set allparams=%*
  set _vspg_dqpacked=%allparams:"=""%
exit /b 0

:UnpackDoubleQuotes
  setlocal & set allparams=%~2
  set unpacked=%allparams:""="%
  endlocal & (set %~1=%unpacked%)
exit /b 0

